#!/bin/awk -f
#This script takes in a listing of variant sites from a VCF
# (e.g. generated by bcftools query -f '%CHROM\t%POS\n')
# and outputs genomic windows that contain approximately
# the same number of sites (except possibly the last window).
BEGIN{
   FS="\t";
   OFS=FS;
   if (length(bandsize) == 0) {
      print "Missing bandsize argument, please specify it. Quitting.\n" > "/dev/stderr";
      exit 2;
   };
   if (bandsize < 1) {
      print "bandsize is too small (< 1), please set it to something reasonable. Quitting.\n" > "/dev/stderr";
      exit 3;
   };
   #Set variables to keep track of previous scaffold and position
   # as well as current band info:
   prevscaf="";
   prevpos=0;
   curbandsize=0;
   bandscaf="";
   bandstart=0;
   bandend=0;
}
NR==1{
   bandscaf=$1;
   bandstart=$2;
   bandend=$2;
   curbandsize++;
   prevscaf=$1;
   prevpos=$2;
}
NR>1{
   if (($1 != prevscaf) || (curbandsize >= bandsize && $2 > prevpos)) {
      print bandscaf, bandstart-1, bandend;
      bandscaf=$1;
      bandstart=$2;
      bandend=$2;
      curbandsize=1;
   } else {
      bandend=$2;
      curbandsize++;
   };
   prevscaf=$1;
   prevpos=$2;
}
END{
   print bandscaf, bandstart-1, bandend;
}
